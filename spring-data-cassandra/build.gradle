buildscript {
    repositories {
        maven { url = 'https://repo.spring.io/snapshot' }
        maven { url = 'https://repo.spring.io/libs-milestone' }
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${versions.springBoot}"
    }
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${versions.springBoot}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${versions.springBoot}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${versions.springBoot}"
    implementation "org.springframework.boot:spring-boot-starter-data-cassandra:${versions.springBoot}"

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.1'
    implementation 'io.prometheus:simpleclient_hotspot:0.16.0'

    // there is a conflict between this version in data-cassandra and web
    // instead of cassandra's 2.13.5, we force the web version 2.20
    implementation('com.fasterxml.jackson.core:jackson-databind') {
        version {
            strictly("2.20.0")
        }
    }

    testImplementation "org.springframework.boot:spring-boot-starter-webmvc-test:${versions.springBoot}"
//    NEXT LINES ARE FOR DOCKER JAVA CLIENT, EXCLUDING VULNERABILITIES AND REDUNDANT DEPENDENCIES
    testImplementation 'com.github.docker-java:docker-java:3.7.0'
    testImplementation 'com.github.docker-java:docker-java-transport-httpclient5:3.7.0'

    test.useJUnitPlatform()
}

jar.enabled=false
bootJar.archiveFileName='service.jar'

test {
    if (project.hasProperty("skipTestsForGithubActions")) {
        exclude '**/RentedMoviesEndpointTests.class'
    }
}
